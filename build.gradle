buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

plugins {
    id 'com.palantir.git-version' version '0.9.1'
    id 'nebula.lint' version '8.3.1'
}

import org.apache.tools.ant.filters.ExpandProperties

if (new File('.git').exists() && (exec {
    commandLine "sh", "-c", "git --version"
    standardOutput = new ByteArrayOutputStream()
    errorOutput = new ByteArrayOutputStream()
    ignoreExitValue true
}).exitValue == 0) {
    version gitVersion()
} else {
    version "unspecified"
}


group 'com.upserve'

description = """Uppend: fast, append-only key-multivalue store"""

task wrapper(type: Wrapper) {
    gradleVersion = '3.3'
}

gradleLint.rules += 'unused-dependency'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'jacoco'
apply plugin: 'com.bmuschko.nexus'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.google.guava:guava:21.0'
    compile 'com.github.ben-manes.caffeine:caffeine:2.3.5'
    compile 'info.picocli:picocli:2.0.1'
    compile 'io.dropwizard.metrics:metrics-core:3.2.3'
    compile 'it.unimi.dsi:fastutil:7.0.13'
    compile 'me.lemire.integercompression:JavaFastPFOR:0.1.11'
    compile 'org.slf4j:slf4j-api:1.7.22'

    testCompile 'junit:junit:4.12'
    testCompile 'org.apache.logging.log4j:log4j-api:2.8'
    testCompile 'org.mockito:mockito-core:2.7.13'

    gradleLint.ignore {
        // gradle-lint doesn't like log4j because it's not used directly
        compile 'org.apache.logging.log4j:log4j-core:2.8'
        compile 'org.apache.logging.log4j:log4j-slf4j-impl:2.8'
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation" << "-Werror"
}

sourceSets {
    main {
        output.resourcesDir = "${buildDir}/classes/main"
    }
    test {
        output.resourcesDir = "${buildDir}/classes/test"
    }
}

ant.property(name: 'gradle_build_version', value: version)

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.properties'
        filter(ExpandProperties, project: ant.antProject)
    }
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Main-Class': 'com.upserve.uppend.Uppend'
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport

modifyPom {
    project {
        name 'Uppend'
        description 'Uppend is an append-only, key-multivalue store.'
        url 'https://github.com/upserve/uppend'
        inceptionYear '2017'

        scm {
            url 'https://github.com/upserve/uppend'
            connection 'scm:https://upserve@github.com/upserve/uppend.git'
            developerConnection 'scm:git://github.com/upserve/uppend.git'
        }

        licenses {
            license {
                name 'The MIT License'
                url 'https://opensource.org/licenses/MIT'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'bfulton'
                name 'Bright Fulton'
                url 'https://github.com/bfulton'
            }
            developer {
                id 'dstuebe'
                name 'David Stuebe'
                url 'https://github.com/dstuebe'
            }
            developer {
                id 'jazzdan'
                name 'Dan Miller'
                url 'https://github.com/jazzdan'
            }
            developer {
                id 'kbarrette'
                name 'Keith Barrette'
                url 'https://github.com/kbarrette'
            }
        }
    }
}
